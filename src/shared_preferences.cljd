(ns shared-preferences
  (:require ["package:shared_preferences/shared_preferences.dart" :as sp]))

(def ^:private instance (atom nil))

(defn init-async
  "Must be called before any data can be persistent. Call it in `main`"
  []
  (reset! instance (await (sp/SharedPreferences.getInstance))))

(defn- get-bool [k-name]
  (.getBool ^sp/SharedPreferences @instance (name k-name)))
(defn- set-bool! [k-name v] (.setBool ^sp/SharedPreferences @instance k-name v))

(defn- get-int [k-name]
  (.getInt ^sp/SharedPreferences @instance (name k-name)))
(defn- set-int! [k-name v] (.setInt ^sp/SharedPreferences @instance k-name v))

(defn- get-double [k-name]
  (.getDouble ^sp/SharedPreferences @instance (name k-name)))
(defn- set-double! [k-name v]
  (.setDouble ^sp/SharedPreferences @instance k-name v))

(defn- get-string [k-name]
  (.getString ^sp/SharedPreferences @instance (name k-name)))
(defn- set-string! [k-name v]
  (.setString ^sp/SharedPreferences @instance k-name v))

(defn- get-keyword [k-name] (keyword (get-string k-name)))
(defn- set-keyword! [k-name v] (set-string! k-name (name v)))

(defn persistent-atom
  "Creates an atom whose data is persistent using SharedPreferences.
  Currently only works with `bool`, `int`, `double`, `String`, `keyword` values.
  `k` is a keyword name assigned to the atom, i.e. `:sound`.
  `v` is the default value.
  NOTE: Any new value of the atom must retain the same value type."
  [k v]
  (let [[getter setter] (cond (boolean? v) [get-bool set-bool!]
                              (int? v) [get-int set-int!]
                              (double? v) [get-double set-double!]
                              (string? v) [get-string set-string!]
                              (keyword? v) [get-keyword set-keyword!])
        k-name (name k)
        val (if (and (some? @instance)
                     (.containsKey ^sp/SharedPreferences @instance k-name))
              (getter k-name)
              v)
        new-atom (atom val)]
    (add-watch new-atom k (fn [_ _ _ new-val]
                            (when (some? @instance) (setter k-name new-val))))))

